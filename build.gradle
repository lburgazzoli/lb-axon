// *****************************************************************************
//
// *****************************************************************************

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'osgi'
    apply plugin: 'maven'
    apply plugin: 'nexus'

    version             = '1.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  
    ext {
        versions = [
            'slf4j'              : '1.7.5',
            'logback'            : '1.1.2',
            'jackson'            : '2.4.1',
            'jacksonAnnotations' : '2.4.1',
            'openhftLang'        : '6.4.5-SNAPSHOT',
            'openhftChronicle'   : '3.2.0-SNAPSHOT',
            'hazelcast'          : '3.2.3',
            'osgi'               : '5.0.0',
            'guava'              : '17.0',
            'axon'               : '2.2',
            'disruptor'          : '3.2.1',
            'junit'              : '4.11',
            'felix'              : '4.4.0',
            'paxExam'            : '4.0.0',
            'paxUrl'             : '2.1.0',
            'jgroups'            : '3.4.4.Final',
            'commonsLang'        : '3.3.2',
            'commonsBeanutils'   : '1.9.1',
            'commonsCollections' : '4.0',
            'javaxCache'         : '1.0.0',
            'spring'             : '3.2.9.RELEASE',
            'groovy'             : '2.3.4',
            'jodaTime'           : '2.3',
	    'vertx'              : '2.1.0',
	    'vertxGroovy'        : '2.1.0-final',
            'lbHazelcast'        : '1.0.0-SNAPSHOT'
        ]

        if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                nexusUsername = sonatypeUsername
                nexusPassword = sonatypePassword
            } else {
                nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
                nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
            }
        }

        gradleVersion    = '2.0'
        gradleScriptDir  = "${rootProject.projectDir}/gradle"
        prefix           = 'lb'
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        gitRoot          = 'https://github.com/lburgazzoli'
        gitProject       = 'https://github.com/lburgazzoli/lb-axon'
    }
   
    repositories {
        mavenLocal()
        mavenCentral()

        maven { url 'http://repo.springsource.org/release/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "org.apache.commons:commons-collections4:$versions.commonsCollections"
        compile "commons-beanutils:commons-beanutils:$versions.commonsBeanutils"
        compile "org.slf4j:slf4j-api:$versions.slf4j"
        compile "org.slf4j:slf4j-ext:$versions.slf4j"
        compile "org.slf4j:jul-to-slf4j:$versions.slf4j"
        compile "org.slf4j:jcl-over-slf4j:$versions.slf4j"        
        compile "org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1"
        compile "org.axonframework:axon-core:$versions.axon"
        compile "org.axonframework:axon-distributed-commandbus:$versions.axon"
        compile "org.jgroups:jgroups:$versions.jgroups"
        compile "org.codehaus.groovy:groovy-all:$versions.groovy"
        compile "joda-time:joda-time:$versions.jodaTime"
        
        testCompile "junit:junit:$versions.junit"
    }

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.prefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = project.version.replaceAll('-','.')
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.prefix}-${project.name}-${project.version}"
        manifest.attributes['Implementation-Version' ] = project.version
        manifest.attributes['Implementation-Vendor'  ] = 'lb'

        manifest.classesDir = new File("$buildDir/classes/main")
        manifest.classpath  = project.runtimeClasspath
    }

    test {
        testLogging {
            events          "failed"
            exceptionFormat "full"
        }
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked",
        "-Xlint:deprecation",
    ]

    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        attachJavadoc = true
        attachSources = true
        attachTests   = false
        sign          = false
    }

    modifyPom {
        project {
            name          = "${project.prefix}-${project.name}"
            description   = "${project.prefix}-${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

