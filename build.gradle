// *****************************************************************************
// -Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'osgi'
    apply plugin: 'maven'
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'com.github.ben-manes.versions'

    version             = '1.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  
    ext {
        versions = [
            'slf4j'              : '1.7.7',
            'log4j2'             : '2.1',
            'jackson'            : '2.4.2',
            'jacksonAnnotations' : '2.4.2',
            'openhftLang'        : '6.4.10',
            'openhftChronicle'   : '3.2.4',
            'hazelcast'          : '3.3.1',
            'osgi'               : '5.0.0',
            'guava'              : '18.0',
            'axon'               : '2.3.2',
            'disruptor'          : '3.3.0',
            'junit'              : '4.11',
            'felix'              : '4.4.1',
            'paxExam'            : '4.3.0',
            'paxUrl'             : '2.2.0',
            'jgroups'            : '3.5.0.Final',
            'commonsLang'        : '3.3.2',
            'commonsBeanutils'   : '1.9.2',
            'commonsCollections' : '4.0',
            'javaxCache'         : '1.0.0',
            'spring'             : '3.2.9.RELEASE',
            'groovy'             : '2.3.7',
            'jodaTime'           : '2.4',
            'lbHazelcast'        : '3.3.1-SNAPSHOT',
            'jdom'               : '2.0.2',
            'xom'                : '1.2.10'
        ]

        if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                nexusUsername = sonatypeUsername
                nexusPassword = sonatypePassword
            } else {
                nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
                nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
            }
        }

        gradleVersion    = '2.1'
        gradleScriptDir  = "${rootProject.projectDir}/gradle"
        prefix           = 'lb'
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        gitRoot          = 'https://github.com/lburgazzoli'
        gitProject       = 'https://github.com/lburgazzoli/lb-axon'
        osgiVersion      = project.version.replaceAll('-','.')
    }
   
    repositories {
        mavenLocal()
        mavenCentral()

        maven { url 'http://repo.springsource.org/release/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$versions.slf4j"
        compile "org.slf4j:slf4j-ext:$versions.slf4j"

        runtime "org.apache.logging.log4j:log4j-api:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-core:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jcl:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jul:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-slf4j-impl:$versions.log4j2"

        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "org.apache.commons:commons-collections4:$versions.commonsCollections"
        compile "commons-beanutils:commons-beanutils:$versions.commonsBeanutils"
        compile "org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1"
        compile "org.axonframework:axon-core:$versions.axon"
        compile "org.axonframework:axon-distributed-commandbus:$versions.axon"
        compile "org.jgroups:jgroups:$versions.jgroups"
        compile "org.codehaus.groovy:groovy-all:$versions.groovy"
        compile "joda-time:joda-time:$versions.jodaTime"

        runtime "com.io7m.xom:xom:$versions.xom"
        runtime "org.jdom:jdom:$versions.jdom"

        testCompile "junit:junit:$versions.junit"
    }

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.prefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = osgiVersion
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.prefix}-${project.name}-${osgiVersion}"
        manifest.attributes['Implementation-Version' ] = osgiVersion
        manifest.attributes['Implementation-Vendor'  ] = 'lb'

        manifest.classesDir = new File("$buildDir/classes/main")
        manifest.classpath  = project.runtimeClasspath
    }

    test {
        testLogging {
            events          "failed"
            exceptionFormat "full"
        }
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked",
        "-Xlint:deprecation",
    ]

    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        attachJavadoc = true
        attachSources = true
        attachTests   = false
        sign          = false
    }

    modifyPom {
        project {
            name          = "${project.prefix}-${project.name}"
            description   = "${project.prefix}-${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

