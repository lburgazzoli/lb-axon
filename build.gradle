
// *****************************************************************************
//
// *****************************************************************************

subprojects {

    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'maven'

    version  = '1.0.0.SNAPSHOT'
    group    = 'com.github.lburgazzoli'

    project.ext.projectPrefix     = 'lb'
    project.ext.slf4jVersion      = '1.7.5'
    project.ext.log4jVersion      = '1.2.17'
    project.ext.jacksonVersion    = '2.3.0'
    project.ext.hazelcastVersion  = '3.1.3'
    project.ext.osgiVersion       = '5.0.0'
    project.ext.guavaVersion      = '15.0'
    project.ext.axonVersion       = '2.0.7'
    project.ext.junitVersion      = '4.11'
    project.ext.felixVersion      = '4.2.1'
    project.ext.paxExamVersion    = '3.4.0'
    project.ext.paxUrlVersion     = '1.6.0'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile "org.apache.commons:commons-lang3:3.1"
        compile "commons-beanutils:commons-beanutils:1.8.3"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:slf4j-ext:$slf4jVersion"
        compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
        compile "log4j:log4j:$log4jVersion"
        compile "com.google.guava:guava:$guavaVersion"
        compile "org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1"
        compile "net.sf.jsr107cache:jsr107cache:1.1"
        compile "org.axonframework:axon-core:$axonVersion"
        compile "org.axonframework:axon-distributed-commandbus:$axonVersion"
        compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

        testCompile "org.apache.felix:org.apache.felix.framework:$felixVersion"
        testCompile "org.ops4j.pax.exam:pax-exam-container-native:$paxExamVersion"
        testCompile "org.ops4j.pax.exam:pax-exam-junit4:$paxExamVersion"
        testCompile "org.ops4j.pax.exam:pax-exam-link-mvn:$paxExamVersion"
        testCompile "org.ops4j.pax.url:pax-url-aether:$paxUrlVersion"
        testCompile "org.ops4j.pax.url:pax-url-reference:$paxUrlVersion"
    }

    jar {
        baseName = "${projectPrefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${projectPrefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${projectPrefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${projectPrefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = project.version
        manifest.attributes['Implementation-Title'   ] = "${projectPrefix}-${project.name}-${project.version}"
        manifest.attributes['Implementation-Version' ] = project.version
        manifest.attributes['Implementation-Vendor'  ] = 'lb'

        manifest.classesDir = new File("$buildDir/classes/main")
        manifest.classpath  = project.runtimeClasspath
    }

    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }

        systemProperties = [
            slf4jVersion      : '1.7.5',
            log4jVersion      : '1.2.17',
            jacksonVersion    : '2.3.0',
            hazelcastVersion  : '3.1.3',
            osgiVersion       : '5.0.0',
            guavaVersion      : '15.0',
            axonVersion       : '2.0.7',
            junitVersion      : '4.11',
            felixVersion      : '4.2.1',
            paxExamVersion    : '3.4.0',
            paxUrlVersion     : '1.6.0'
        ]
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked"
    ]
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

